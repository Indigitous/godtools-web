# A big object containing meta info about all the available locales and their booklets.
gt.locale_meta = <%= app.locale_meta.to_json %>


# Try to find the meta for a specific locale if it's supported by the app, otherwise return null.
gt.meta_for_locale = (locale) ->
  if typeof gt.locale_meta[locale] == 'object'
    gt.locale_meta[locale]
  else if typeof gt.locale_meta[locale.substr(0, 2)] == 'object' # If the locale has a region like "en-US" then try ignoring the region.
    gt.locale_meta[locale.substr(0, 2)]
  else
    null


# Examples of navigator language codes include "en", "en-US", "fr", "es-ES", etc.
gt.get_navigator_language = ->
  if navigator.languages?
    navigator.languages[0]
  else
    (navigator.language || navigator.userLanguage)


# Look at the navigator language and try to return a locale that the app supports.
gt.get_desired_locale = ->
  navigator_language = gt.get_navigator_language()
  if gt.meta_for_locale(navigator_language) != null
    navigator_language.substr(0, 2) # Only return first two characters in case a region is present, like "en-US".
  else
    null


gt.redirect_to_current_path_in_locale = (desired_locale) ->
  redirect_url = "/#{ desired_locale }#{ window.location.pathname }"
  window.location.replace redirect_url
  console.log "redirected to #{ redirect_url }"


gt.display_locale_switch_alert_for_locale = (desired_locale) ->
  re = new RegExp "^\/#{ gt.current_locale }", 'g'
  redirect_to_locale_url = window.location.pathname.replace re, "/#{ desired_locale }"
  $ ->
    if desired_locale != gt.current_locale
      $(HandlebarsTemplates['alerts/switch_language']({
        url: redirect_to_locale_url,
        language_name: gt.meta_for_locale(desired_locale).language_name
      })).appendTo('.navbar-right')


gt.is_current_booklet_available_in_current_locale = ->
  gt.meta_for_locale(gt.current_locale).booklets.indexOf(gt.current_booklet) >= 0


# Called on every page load to setup the locale.
gt.initialize_locale = ->
  console.log 'Initializing locale'

  desired_locale = gt.get_desired_locale()

  # If we can't find a desired locale then do nothing.
  if desired_locale == null
    console.log "Navigator language unsupported: #{ desired_locale }"
    return

  # If the current locale is unspecified then we can simple redirect to the desired locale.
  if gt.current_locale == 'unspecified'
    gt.redirect_to_current_path_in_locale desired_locale
    return

  else # If the current locale is already specified then we will prompt the user to switch locales.
    # If the translation is not available then we should apologize.
    if gt.current_booklet != '' and !gt.is_current_booklet_available_in_current_locale()
      $ ->
        $(HandlebarsTemplates['alerts/no_translation_apology']({
          language_name: gt.meta_for_locale(gt.current_locale).language_name_in_english
        })).appendTo('.alerts')
      gt.display_locale_switch_alert_for_locale(desired_locale) if desired_locale != 'en'
    else
      gt.display_locale_switch_alert_for_locale(desired_locale)

